#!/usr/local/bin/perl
    eval 'exec /usr/local/bin/perl -S $0 ${1+"$@"}'
        if $running_under_some_shell;
#!/usr/bin/perl
#
# $Id$
#
use 5.008001;
use strict;
use warnings;
use utf8;
use open ':utf8';
binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';
use Encode;
use Text::Itrans;
our $VERSION = sprintf "%.2f", (q$Revision$ =~ /(\d+)/g)[0] / 100;
our $DEBUG = 0;

use File::Basename;
my $name = basename($0);
our $NAME = 'Text::' . ucfirst($name);

use Getopt::Long qw(:config no_ignore_case);
GetOptions(\%ARGV, qw(
    from-lang=s to-lang=s
    list
    output=s silent verbose
    help|?   usage  version|V
)) or usage();
$DEBUG++ if     $ARGV{verbose};
version() if     $ARGV{version};
usage()   if     $ARGV{usage};
help()    if     $ARGV{help};
usage()   unless $ARGV{'from-lang'};
usage()   unless $ARGV{'to-lang'};

my $utf8 = find_encoding('utf8');
my $translator = Text::Itrans->new(
    $ARGV{'from-lang'}, $ARGV{'to-lang'}
);
$| = 1;
while (<>) {
    chomp;
    for (my $i = 0, my $l = my $j = length; $i < $l; $j--) {
        my $query = substr $_, $i, $j;
        $DEBUG and warn $query;
        my $result =
            $utf8->decode(
                $translator->translate($query) || ''
            );
        print $result 
           || ($j == 1 && $query)
           || next;
        $DEBUG and print "\n";
        $i += $j;
        $j = $l - $i + 1;
    }
    print "\n" unless $DEBUG;
}

sub help{
    print <<"EOT";
Usage: $name [OPTION...] [FILE...]
Translate language of given files from one language to another.

 Input/Output format specification:
  -f, --from-lang=NAME       language of original text
  -t, --to-lang=NAME         language for output

 Information:
  -l, --list                 list all known languages

 Output control:
  -o, --output=FILE          output file
  -s, --silent               suppress warnings
      --verbose              print progress information

  -?, --help                 Give this help list
      --usage                Give a short usage message
  -V, --version              Print program version

Mandatory or optional arguments to long options are also mandatory or optional
for any corresponding short options.

For bug reporting instructions, please see:
<http://rt.cpan.org/NoAuth/ReportBug.html?Queue=Text-Itrans>.
EOT
    exit;
}

sub usage{
    print <<"EOT";
Usage: $name [-ls?V] [-f NAME] [-t NAME] [-o FILE] [--from-lang=NAME]
            [--to-lang=NAME] [--list] [--output=FILE] [--silent] [--verbose]
            [--help] [--usage] [--version] [FILE...]
EOT
    exit;
}

sub version{
    print <<"EOT";
$name ($NAME) $VERSION
Copyright (C) 2009 Konno Software.
This is free software; see the soruce for copying conditions.  There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
Written by Yuki Konno.
EOT
    exit;
}

__END__

=head1 NAME

itrans - Translate language of given files from one language to another

=head1 SYNOPSIS

    itrans -f language -t language [inputfile]...

=head1 DESCRIPTION

The B<itrans> program converts the language of text in inputfile, or
from the standard input if no filename is specified, from one
language to another. The result is written to standard output
unless otherwise specified by the --output option.

=over 4

=item --from-lang, -f language

Translate text from language.

=item --to-code, -t language

Translate text to language. If not specified the language
corresponding to the current locale is used.

=item --list, -l

List known languages.

=item --output, -o file

Specify output file (instead of stdout).

=item --silent, -s

Suppress warnings, but not errors.

=item --verbose

Print progress information.

=item --help, -?

Give help list.

=item --usage

Give a short usage message.

=item --version, -V

Print program version.

=back

=head1 SEE ALSO

L<Text-Itrans>

=cut
